/**
 *
 */
macroscript core_group__createGroupByService(
	/** Group id */ @NotNull string id,
	/** Group name */ @NotNull string name,
	/** Group members */ @NotNull array members,
	/** Group metadata */ map metadata = {},
	/** Group tags */ array tags = [],
	/** Group service implementation */ @NotNull service<groups> zpService
) {
	trace(#core_group__createGroupByService, { id, name, members, metadata, tags });
	/** Merge metadata */
	metadata += {
		createdAt: time:now(),
		type: metadata.type ?: DEFAULT_GROUP_TYPE
	};

	const existence = zpService.exists({
		group: id
	});

	const resource = str:join(':', __userKey, id);

	/** Avoid error if group already exist */
	if (!existence.exists) {
		/** Create group */
		zpService.createGroup({
			group: id,
			groupName: name
		});

		/** Set group metadata */
		await zpRecipeUtils::setMetadata({ id, metadata });
		
		/** Set group tags */
		await zpRecipeUtils::setTags({ id, tags });
	}
	/** Allow members to list members */
	zpService.grant({
		action: 'LIST',
		group: id,
		resource: resource
	});
	
	/** Add initial users to the group */
	zpService.addUsers({
		group: id,
		users: members
	});

	/** Get the created group */
	const { group } = await core_group__getGroupByService({ id, zpService });

	const userKey = __userKey;

	/** Execute all registered macros */
	zpRecipeUtils::zpServiceTrigger.trigger({
		event: EVENT__CREATE_GROUP,
		data: { group, userKey }
	});
} broadcast {
	Group group
} on channel __selfName
